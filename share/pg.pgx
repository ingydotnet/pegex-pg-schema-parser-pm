%grammar pg
%version 0.0.1

pg-schema-dump: sql-statement*

sql-statement:
| blank-lines
| comment-lines
| alter-statement
| create-function-statement
| create-other-statement
| create-table-statement
| create-view-statement
| other-statement

blank-lines: / blank-line+ /
comment-lines: / comment-line+ /
alter-statement: / 'ALTER' + ( WORD+) + ( ALL*? ) end /
create-table-statement: / 'CREATE' + 'TABLE' + ( ALL*? ) end /
create-view-statement: / 'CREATE' + 'VIEW' + ( ALL*? ) end /
create-function-statement: /
  'CREATE' + 'FUNCTION' +
  ( function-part*? )
  end
/
create-other-statement: /
  'CREATE' +
  ( 'AGGREGATE'
  | 'EXTENSION'
  | (:'UNIQUE' + )? 'INDEX'
  | 'OPERATOR'
  | 'SEQUENCE'
  | 'TYPE'
  | (:'CONSTRAINT' + )? 'TRIGGER'
  )
  +
  ( ALL*? )
  end
/
other-statement: / (SET|COMMENT|GRANT|REVOKE|TYPE) + ( ALL*? ) end /

function-part: /(:
  (: 'DECLARE' ALL*? EOL (='BEGIN') )?
  (: 'BEGIN' ALL*? EOL 'END' end ) |
  (: ALL*? (=';'|DECLARE|BEGIN) )
)/

blank-line: /(: SPACE* EOL )/
comment-line: /(: '--' ANY* EOL )/
end: /- SEMI SPACE* EOL /
